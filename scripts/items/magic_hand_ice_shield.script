//Ice Shield
//Level req: 5
//50% damage reduction

{
	const NO_REGISTER 1
	const SOUND_SHOOT magic/cast.wav
	const ICESHIELD_RANGE 389 //Opted for slightly higher range because it's not origin to origin, it's the actual range of the trace from anywhere on an entity

	const MELEE_ATK_DURATION 0.65

	const MANA_COST	50 //special internal handling
	const ICESHIELD_FORMULA 0.5 //50% damage reduction

	const SPELL_SCRIPT effects/iceshield
}

#include items/magic_hand_base

{ spell_spawn

   name "Ice Shield"
   desc "Provides 50% damage reduction for you or allies, for a time."
   
   setvard LAST_ATTACK $math(add,game.time,MELEE_ATK_DURATION)
}

{ game_attack1

	local TIME_DIFF game.time
	subtract TIME_DIFF LAST_ATTACK
	
	if TIME_DIFF > MELEE_ATK_DURATION

	setvard LAST_ATTACK game.time

	playviewanim ANIM_CAST
	playowneranim critical PLAYERANIM_PREPARE squatwalk1

	if ( $get(ent_owner,mp) < MANA_COST ) dplayermessage ent_owner "Insufficient mana."
	if $get(ent_owner,mp) >= MANA_COST

	//local SPELL_TARGET $get(ent_owner,target)
	local SPELL_TARGET $func(func_get_ray_target,ent_owner,ICESHIELD_RANGE)

	//is this target valid, if not, make owner target
	if ( !$get(SPELL_TARGET,isalive) ) local SPELL_TARGET $get(ent_owner,id)
	if ( !$get(SPELL_TARGET,isplayer) )
	{
		if $get(ent_owner,relationship,SPELL_TARGET) isnot ally
		local SPELL_TARGET $get(ent_owner,id)
	}

	local FINAL_DURATION 150
	if ( $get(SPELL_TARGET,id) isnot $get(ent_owner,id) ) local FINAL_DURATION 412

	givemp $neg(MANA_COST)
	callexternal ent_owner mana_drain //update mana on player hud readout, if using hud feedback
	
	local ALREADY_SHIELDED $get(SPELL_TARGET,haseffect,iceshield)
	if ( ALREADY_SHIELDED ) //If target is already shielded, refresh their duration.
	{
		callexternal SPELL_TARGET ext_refresh_ice_shield FINAL_DURATION $get(ent_owner,id) //<duration> <shielder>
	}
	else
	{
		applyeffect SPELL_TARGET SPELL_SCRIPT FINAL_DURATION $get(ent_owner,id) ICESHIELD_FORMULA //<duration> <shielder> <damage_reduction>
	}
}

{ func_get_ray_target //<player> <range> returns eyetarget entity
	local PLR PARAM1
	local PLR_ID $get(PLR,id)
	local PLR_ORG $get(PLR,eyepos)
	local PLR_ANG $get(PLR,viewangles)
	if ( $get(PLR,sitting) == 1 ) vectoradd PLR_ORG z -27 //account for where eyes are
	local TRACE_START PLR_ORG
	local TRACE_END PLR_ORG
	vectoradd TRACE_END $relpos(PLR_ANG,$vec(0,PARAM2,0))
	local TRACED_LINE $get_traceline(TRACE_START,TRACE_END,ent,PLR_ID)

	//DEBUG
	effect beam point laserbeam.spr 20 TRACE_START TRACE_END (255,255,0) 255 0.7 30

	return **clear
	return TRACED_LINE
}